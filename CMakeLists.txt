project(PolyJIT)
cmake_minimum_required(VERSION 3.8)

include(ExternalProject)

set(POLYJIT_DISABLE_AUTO_UPDATE 0 CACHE STRING "Disable auto update of projects.")
set(POLYJIT_BRANCH_LLVM "master" CACHE STRING "Branch of LLVM to use.")
set(POLYJIT_BRANCH_CLANG "master" CACHE STRING "Branch of Clang to use.")
set(POLYJIT_BRANCH_POLLY "master" CACHE STRING "Branch of Polly to use.")
set(POLYJIT_BRANCH_POLLI "master" CACHE STRING "Branch of Polli to use.")

set(POLYJIT_REPO_LLVM "https://github.com/PolyJIT/llvm.git" CACHE STRING
    "Repository of LLVM to use.")
set(POLYJIT_REPO_CLANG "https://github.com/PolyJIT/clang.git" CACHE STRING
    "Repository of Clang to use.")
set(POLYJIT_REPO_POLLY "https://github.com/PolyJIT/polly.git" CACHE STRING
    "Repository of Polly to use.")
set(POLYJIT_REPO_POLLI "https://github.com/PolyJIT/polli.git" CACHE STRING
    "Repository of Polli to use.")

set(POLYJIT_PAPI_PREFIX "/usr" CACHE STRING "Installation prefix of libpapi")

set(BUILD_SHARED_LIBS On CACHE BOOL "Enable/Disable shared libs.")
set(POLYJIT_CMAKE_ARGS
  -DCMAKE_CXX_COMPILER=${CMAKE_CXX_COMPILER}
  -DCMAKE_CXX_FLAGS=${CMAKE_CXX_FLAGS}
  -DCMAKE_CXX_FLAGS_RELEASE=${CMAKE_CXX_FLAGS_RELEASE}
  -DCMAKE_CXX_FLAGS_RELWITHDEBINFO=${CMAKE_CXX_FLAGS_RELWITHDEBINFO}
  -DCMAKE_CXX_FLAGS_DEBUG=${CMAKE_CXX_FLAGS_DEBUG}
  -DCMAKE_C_COMPILER=${CMAKE_C_COMPILER}
  -DCMAKE_BUILD_TYPE=${CMAKE_BUILD_TYPE}
  -DCMAKE_EXPORT_COMPILE_COMMANDS=${CMAKE_EXPORT_COMPILE_COMMANDS}
  -DCMAKE_INSTALL_PREFIX=${CMAKE_INSTALL_PREFIX}
  -DCMAKE_LINKER=${CMAKE_LINKER}
  -DLLVM_POLLY_LINK_INTO_TOOLS=OFF
  -DLLVM_TARGETS_TO_BUILD=X86
  -DLLVM_ENABLE_TERMINFO=OFF
  -DBUILD_SHARED_LIBS=${BUILD_SHARED_LIBS}
)

set(POLYJIT_PROJECT_DIR ${CMAKE_CURRENT_BINARY_DIR})
set(POLYJIT_BUILD_DIR "${POLYJIT_PROJECT_DIR}/build")
set(POLYJIT_PREFIX_DIR "${POLYJIT_PROJECT_DIR}/prefix")

ExternalProject_Add(clang
  GIT_REPOSITORY ${POLYJIT_REPO_CLANG}
  GIT_TAG ${POLYJIT_BRANCH_CLANG}
  GIT_SHALLOW 1

  STEP_TARGETS update
  INDEPENDENT_STEP_TARGETS download
  UPDATE_DISCONNECTED ${POLYJIT_DISABLE_AUTO_UPDATE}

  SOURCE_DIR "${POLYJIT_PROJECT_DIR}/clang"
  PREFIX "${POLYJIT_PREFIX_DIR}/clang"

  CONFIGURE_COMMAND ""
  BUILD_COMMAND ""
  INSTALL_COMMAND ""
  )

ExternalProject_Get_Property(clang SOURCE_DIR)
set(CLANG_SOURCE_ROOT ${SOURCE_DIR})

ExternalProject_Add(polly
  GIT_REPOSITORY ${POLYJIT_REPO_POLLY}
  GIT_TAG ${POLYJIT_BRANCH_POLLY}
  GIT_SHALLOW 1

  STEP_TARGETS update
  INDEPENDENT_STEP_TARGETS download
  UPDATE_DISCONNECTED ${POLYJIT_DISABLE_AUTO_UPDATE}

  SOURCE_DIR "${POLYJIT_PROJECT_DIR}/polly"
  PREFIX "${POLYJIT_PREFIX_DIR}/polly"

  CONFIGURE_COMMAND ""
  BUILD_COMMAND ""
  INSTALL_COMMAND ""
  )

ExternalProject_Get_Property(polly SOURCE_DIR)
set(POLLY_SOURCE_ROOT ${SOURCE_DIR})

ExternalProject_Add(llvm
  DEPENDS clang polly
  GIT_REPOSITORY ${POLYJIT_REPO_LLVM}
  GIT_TAG ${POLYJIT_BRANCH_LLVM}
  GIT_SHALLOW 1
  STEP_TARGETS update build configure install
  UPDATE_DISCONNECTED ${POLYJIT_DISABLE_AUTO_UPDATE}

  SOURCE_DIR "${POLYJIT_PROJECT_DIR}/llvm"
  BINARY_DIR "${POLYJIT_BUILD_DIR}/llvm"
  PREFIX "${POLYJIT_PREFIX_DIR}/llvm"
  CMAKE_ARGS
    ${POLYJIT_CMAKE_ARGS}
  )

ExternalProject_Get_Property(llvm SOURCE_DIR)
set(LLVM_SOURCE_ROOT ${SOURCE_DIR})

ExternalProject_Add_Step(llvm link_clang
  COMMAND ln -sf ${CLANG_SOURCE_ROOT} ${LLVM_SOURCE_ROOT}/tools/clang
  COMMENT "Link clang into LLVM source tree"
  DEPENDEES download
  DEPENDERS configure
  WORKING_DIRECTORY ${LLVM_SOURCE_ROOT}
)

ExternalProject_Add_Step(llvm link_polly
  COMMAND ln -sf ${POLLY_SOURCE_ROOT} ${LLVM_SOURCE_ROOT}/tools/polly
  COMMENT "Link Polly into LLVM source tree"
  DEPENDEES download
  DEPENDERS configure
  WORKING_DIRECTORY ${LLVM_SOURCE_ROOT}
)

set(LLVM_INSTALL_ROOT ${CMAKE_INSTALL_PREFIX})
set(POLLY_INSTALL_ROOT ${CMAKE_INSTALL_PREFIX})

ExternalProject_Add(polli
  DEPENDS llvm polly
  GIT_REPOSITORY ${POLYJIT_REPO_POLLI}
  GIT_TAG ${POLYJIT_BRANCH_POLLI}
  STEP_TARGETS update build configure install
  INDEPENDENT_STEP_TARGETS download
  UPDATE_DISCONNECTED ${POLYJIT_DISABLE_AUTO_UPDATE}

  SOURCE_DIR "${POLYJIT_PROJECT_DIR}/polli"
  BINARY_DIR "${POLYJIT_BUILD_DIR}/polli"
  PREFIX "${POLYJIT_PREFIX_DIR}/polli"
  CMAKE_ARGS
    -DBUILD_SHARED_LIBS=On
    -DLLVM_DIR=${LLVM_INSTALL_ROOT}
    -DLLVM_INSTALL_ROOT=${LLVM_INSTALL_ROOT}
    -DPOLLY_INSTALL_ROOT=${POLLY_INSTALL_ROOT}
    -DPAPI_INCLUDE_DIR=${POLYJIT_PAPI_PREFIX}/include
    -DPAPI_LIBRARY=${POLYJIT_PAPI_PREFIX}/lib/libpapi.so
    -DPAPI_INSTALL_ROOT=${POLYJIT_PAPI_PREFIX}
    ${POLYJIT_CMAKE_ARGS}
  )
